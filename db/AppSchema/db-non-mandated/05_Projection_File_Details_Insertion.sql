------TAKING THE BACKUP OF THE EXISTING DATA
IF NOT EXISTS (
		SELECT 'X'
		FROM SYS.TABLES
		WHERE Object_name(OBJECT_ID) = 'PROJECTION_FILE_DETAILS_BKUP'
			AND Schema_name(SCHEMA_ID) = 'dbo'
		)
BEGIN
	CREATE TABLE [DBO].[PROJECTION_FILE_DETAILS_BKUP] (
		[PROJECTION_MASTER_SID] [int] NOT NULL
		,[FILE_MANAGEMENT_SID] [int] NOT NULL
		,[FILE_TYPE] [int] NOT NULL
		,[SCREEN_NAME] [char](1) NULL
		,[FLAG] [bit] NULL
		)
END
GO

IF NOT EXISTS (
		SELECT 1
		FROM PROJECTION_FILE_DETAILS_BKUP
		)
BEGIN
	INSERT INTO [PROJECTION_FILE_DETAILS_BKUP] (
		PROJECTION_MASTER_SID
		,FILE_MANAGEMENT_SID
		,FILE_TYPE
		,SCREEN_NAME
		,FLAG
		)
	SELECT PROJECTION_MASTER_SID
		,FILE_MANAGEMENT_SID
		,FILE_TYPE
		,SCREEN_NAME
		,FLAG
	FROM PROJECTION_FILE_DETAILS
END
GO

;

WITH cte
AS (
	SELECT DISTINCT pd.PROJECTION_MASTER_SID
		,CASE 
			WHEN METHODOLOGY IN (
					'Single Period'
					,'Average'
					,'ROLLING ANNUAL TREND'
					)
				THEN 'Ex-Factory Sales'						
			WHEN METHODOLOGY = '% of Ex-Factory Sales'
				THEN 'Ex-Factory Sales'
			WHEN METHODOLOGY = '% OF INVENTORY WITHDRAWAL'
				THEN 'Inventory Withdrawal - Forecast Summary'
			WHEN METHODOLOGY = '% OF DEMAND'
				THEN 'Demand'
			WHEN METHODOLOGY = '% OF ADJUSTED DEMAND'
				THEN 'Adjusted Demand'
			WHEN METHODOLOGY = 'CUSTOMER GTS'
				THEN 'Customer Sales'
			END file_type
		,'S' AS SCREEN_NAME
	FROM PROJECTION_DETAILS pd
	JOIN NM_SALES_PROJECTION_MASTER mas ON mas.PROJECTION_DETAILS_SID = pd.PROJECTION_DETAILS_SID
	)
UPDATE pfd
SET flag = 1
	,SCREEN_NAME = 'S'
FROM cte c
JOIN HELPER_TABLE h ON h.DESCRIPTION = c.file_type
	AND h.LIST_NAME = 'file_type'
JOIN PROJECTION_FILE_DETAILS pfd ON pfd.PROJECTION_MASTER_SID = c.PROJECTION_MASTER_SID
	AND pfd.FILE_TYPE = h.HELPER_TABLE_SID
	AND NOT EXISTS (
		SELECT 1
		FROM PROJECTION_FILE_DETAILS pr
		WHERE pr.PROJECTION_MASTER_SID = pfd.PROJECTION_MASTER_SID
			AND PFD.FILE_TYPE = PR.FILE_TYPE
			AND pr.SCREEN_NAME = 'S'
		)
GO

-----------------updating discount
	;

WITH cte2
AS (
	SELECT DISTINCT pd.PROJECTION_MASTER_SID
		,CASE 
			WHEN METHODOLOGY IN (
					'Single Period'
					,'Average'
					,'ROLLING ANNUAL TREND'
					)
				THEN 'Ex-Factory Sales'						
			END file_type
		,'D' AS SCREEN_NAME
	FROM PROJECTION_DETAILS pd
	JOIN NM_SALES_PROJECTION_MASTER mas ON mas.PROJECTION_DETAILS_SID = pd.PROJECTION_DETAILS_SID
	WHERE METHODOLOGY IN (
			'Single Period'
			,'Average'
			,'ROLLING ANNUAL TREND'
			)
	
	UNION ALL
	
	SELECT DISTINCT pd.PROJECTION_MASTER_SID
		,CASE 
			WHEN METHODOLOGY = '% of Ex-Factory Sales'
				THEN 'Ex-Factory Sales'
			WHEN METHODOLOGY = '% OF INVENTORY WITHDRAWAL'
				THEN 'Inventory Withdrawal - Forecast Summary'
			WHEN METHODOLOGY = '% OF DEMAND'
				THEN 'Demand'
			WHEN METHODOLOGY = '% OF ADJUSTED DEMAND'
				THEN 'Adjusted Demand'
			WHEN METHODOLOGY = 'CUSTOMER GTS'
				THEN 'Customer Sales'
			END
		,'D' AS SCREEN_NAME
	FROM PROJECTION_DETAILS pd
	JOIN NM_discount_PROJ_MASTER mas ON mas.PROJECTION_DETAILS_SID = pd.PROJECTION_DETAILS_SID
		AND METHODOLOGY NOT IN (
			'Single Period'
			,'Average'
			,'ROLLING ANNUAL TREND'
			,'Contract Details'
			)
	)
INSERT INTO PROJECTION_FILE_DETAILS (
	PROJECTION_MASTER_SID
	,FILE_MANAGEMENT_SID
	,FILE_TYPE
	,SCREEN_NAME
	,FLAG
	)
SELECT distinct pfd.PROJECTION_MASTER_SID
	,pfd.FILE_MANAGEMENT_SID
	,pfd.FILE_TYPE
	,'D' AS SCREEN_NAME
	,1
FROM CTE2 C
JOIN HELPER_TABLE H ON H.DESCRIPTION = C.FILE_TYPE
	AND H.LIST_NAME = 'FILE_TYPE'
JOIN (
	SELECT *
	FROM PROJECTION_FILE_DETAILS
	WHERE SCREEN_NAME = 'S'
	) pfd ON pfd.PROJECTION_MASTER_SID = c.PROJECTION_MASTER_SID
	AND pfd.FILE_TYPE = h.HELPER_TABLE_SID
	AND NOT EXISTS (
		SELECT 1
		FROM PROJECTION_FILE_DETAILS pr
		WHERE pr.PROJECTION_MASTER_SID = pfd.PROJECTION_MASTER_SID
			AND PFD.FILE_TYPE = PR.FILE_TYPE
			AND pr.SCREEN_NAME = 'D'
		)
GO



----------------DELETING THE RECORDS WHICH DOESN'T SAVED OR THE FILE NOT USED IN PROJECTION
IF EXISTS (
		SELECT 1
		FROM PROJECTION_FILE_DETAILS
		WHERE SCREEN_NAME IS NULL
		)
BEGIN
	DELETE
	FROM PROJECTION_FILE_DETAILS
	WHERE SCREEN_NAME IS NULL
END
GO
----------------------------------------------------

IF EXISTS (SELECT 1
              FROM   INFORMATION_SCHEMA.COLUMNS
              WHERE  TABLE_NAME = 'PROJECTION_FILE_DETAILS'
                     AND COLUMN_NAME = 'SCREEN_NAME'
                     AND TABLE_SCHEMA = 'DBO')
  BEGIN
      ALTER TABLE [DBO].PROJECTION_FILE_DETAILS
        ALTER COLUMN SCREEN_NAME CHAR(1) NOT NULL
  END

GO 

-----------------------PK------------------------------------------------------- 

IF NOT EXISTS(SELECT 1 
              FROM   SYS.KEY_CONSTRAINTS 
              WHERE  OBJECT_NAME(PARENT_OBJECT_ID) = 'PROJECTION_FILE_DETAILS' 
                     AND SCHEMA_NAME(SCHEMA_ID) = 'DBO' 
                     AND NAME = 'PK_PROJECTION_FILE_DETAILS_PROJECTION_MASTER_SID_FILE_MANAGEMENT_SID_FILE_TYPE_SCREEN_NAME'
                     AND TYPE = 'PK') 
  BEGIN 
      ALTER TABLE [DBO].[PROJECTION_FILE_DETAILS] 
        ADD CONSTRAINT PK_PROJECTION_FILE_DETAILS_PROJECTION_MASTER_SID_FILE_MANAGEMENT_SID_FILE_TYPE_SCREEN_NAME PRIMARY KEY(PROJECTION_MASTER_SID,FILE_MANAGEMENT_SID,FILE_TYPE,SCREEN_NAME)
  END 
GO